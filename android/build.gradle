// There is no version defined on the package.
// Since Capacitor 5 the minimum Java version was altered to version 18 so we will use this information to define if
// the project is running on Capacitor 5 or higher.
def cap5OrHigher =  rootProject.project(':capacitor-android').android.compileOptions.sourceCompatibility >= JavaVersion.VERSION_17

def defaultSdkVersion = 33
def targetJavaVersion = JavaVersion.VERSION_17
if (cap5OrHigher) {
  println "Sentry Capacitor: Applying defaults for Capacitor 5 or higher"
  ext {
    junitVersion =  project.hasProperty('junitVersion') ? rootProject.ext.junitVersion : '4.13.2'
    androidxAppCompatVersion = project.hasProperty('androidxAppCompatVersion') ? rootProject.ext.androidxAppCompatVersion : '1.6.1'
    androidxJunitVersion = project.hasProperty('androidxJunitVersion') ? rootProject.ext.androidxJunitVersion : '1.1.5'
    androidxEspressoCoreVersion = project.hasProperty('androidxEspressoCoreVersion') ? rootProject.ext.androidxEspressoCoreVersion : '3.5.1'
  }
}
else {
  println "Sentry Capacitor: Applying defaults for Capacitor 4."
  defaultSdkVersion = 32
  targetJavaVersion = JavaVersion.VERSION_11
  ext {
    junitVersion =  project.hasProperty('junitVersion') ? rootProject.ext.junitVersion : '4.12'
    androidxJunitVersion = project.hasProperty('androidxJunitVersion') ? rootProject.ext.androidxJunitVersion : '1.1.1'
    androidxEspressoCoreVersion = project.hasProperty('androidxEspressoCoreVersion') ? rootProject.ext.androidxEspressoCoreVersion : '3.2.0'
  }
}

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    if (rootProject.project(':capacitor-android').android.compileOptions.sourceCompatibility >= JavaVersion.VERSION_17) {
      println "Sentry Capacitor: Applying dependencies for Capacitor 5 or higher."
      classpath 'com.android.tools.build:gradle:8.0.0'
      classpath 'com.google.gms:google-services:4.3.15'
    }
    else {
      println "Sentry Capacitor: Applying dependencies for Capacitor 4."
      classpath 'com.android.tools.build:gradle:7.2.1'
      classpath 'com.google.gms:google-services:4.3.13'
    }
  }
}

apply plugin: 'com.android.library'

android {
  namespace "io.sentry.capacitor"
  compileSdkVersion project.hasProperty('compileSdkVersion') ? rootProject.ext.compileSdkVersion : defaultSdkVersion
  defaultConfig {
    minSdkVersion project.hasProperty('minSdkVersion') ? rootProject.ext.minSdkVersion : 22
    targetSdkVersion project.hasProperty('targetSdkVersion') ? rootProject.ext.targetSdkVersion : defaultSdkVersion
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    targetCompatibility targetJavaVersion
    sourceCompatibility targetJavaVersion
  }
}

repositories {
  google()
  mavenCentral()
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':capacitor-android')
    implementation 'io.sentry:sentry-android:6.19.0'
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
}

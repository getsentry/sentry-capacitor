def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def getCapacitorCompileSdkVersion() {
  return  Integer.parseInt(
    rootProject.project(':capacitor-android').android.compileSdkVersion
      .replaceAll(/\D+/, ''))
}

def GetCapacitorVersion() {
  def compileVersion =  getCapacitorCompileSdkVersion()
  if (compileVersion >= 34) {
    return 6
  }
  else if (compileVersion == 33) {
    return 5
  }
  else if (compileVersion == 32 || compileVersion == 31) {
    return 4
  }
  else if (compileVersion == 30 || compileVersion == 29) {
    return 3
  }
  else {
    throw new GradleException("The version of Capacitor is not supported by Sentry, CODE: " + compileVersion)
  }
}

def capVersion = GetCapacitorVersion()

def defaultSdkVersion = getCapacitorCompileSdkVersion()
def targetJavaVersion = JavaVersion.VERSION_17
if (capVersion >= 5) {
  println "Sentry Capacitor: Applying defaults for Capacitor 5 or higher"
  ext {
    androidxAppCompatVersion = safeExtGet('androidxAppCompatVersion', '1.6.1')
    androidxEspressoCoreVersion = safeExtGet('androidxEspressoCoreVersion', '3.5.1')
  }
}
else {
  println "Sentry Capacitor: Applying defaults for Capacitor 4."
  targetJavaVersion = JavaVersion.VERSION_11
  ext {
    androidxEspressoCoreVersion = safeExtGet('androidxEspressoCoreVersion', '3.2.0')
  }
}

apply plugin: 'com.android.library'

android {
  namespace "io.sentry.capacitor"
  compileSdkVersion safeExtGet('compileSdkVersion', defaultSdkVersion)
  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 22) // Same for Capacitor 6 or lower
    targetSdkVersion safeExtGet('targetSdkVersion', defaultSdkVersion)
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    targetCompatibility targetJavaVersion
    sourceCompatibility targetJavaVersion
  }
}

repositories {
  google()
  mavenCentral()
}

dependencies {
    implementation project(':capacitor-android')
    implementation 'io.sentry:sentry-android:8.0.0'
}
